buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.0.0')
    }
}

group = 'org.sample'
version = currentVersion

apply plugin: 'com.jfrog.artifactory'
apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
     maven { url "${artifactoryUrl}/artifactory/libs-release-local" }
     maven { url "http://jcenter.bintray.com" }
     maven { url "${artifactoryUrl}/artifactory/libs-snapshot-local" }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'log4j:log4j:1.2.17'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

test {
    testLogging {
    	events "passed", "skipped", "failed", "standardOut", "standardError"
    	afterSuite { desc, result ->
			if (!desc.parent) {
				println "Results: ${result.resultType} (Total: ${result.testCount}, Passed: ${result.successfulTestCount}, Failed: ${result.failedTestCount}, Skipped: ${result.skippedTestCount})"
			}
		}
    }
}

artifactory {
    contextUrl = "${artifactoryUrl}/artifactory"
    publish {
        repository {
            repoKey = 'libs-snapshot-local'
            username = "${artifactoryUser}"
            password = "${artifactoryPassword}"
        }
        repository {
            repoKey = 'libs-release-local'
            username = "${artifactoryUser}"
            password = "${artifactoryPassword}"
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            properties = ['qa.level': 'basic', 'dev.team' : 'core']
            publishPom = true
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
}
